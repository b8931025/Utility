有時會遇到多階層關係的資料，可能會想知道層級，這時就可以用CTE遞迴來實現。

測試資料建立：
--存在則刪除
IF OBJECT_ID('EMPLOYEE', 'U') IS NOT NULL
	DROP TABLE EMPLOYEE;
GO

--建立員工資料表
CREATE TABLE EMPLOYEE
(
	DEPT_NO VARCHAR(10) NOT NULL,
	EMPLOYEE_NO   VARCHAR(10) NOT NULL,
	EMPLOYEE_NAME NVARCHAR(20) NOT NULL,
	TITLE NVARCHAR(20) NOT NULL,
	SUPERIOR_NO VARCHAR(20) NOT NULL
);

--寫入員工資料
INSERT INTO EMPLOYEE (DEPT_NO,EMPLOYEE_NO, EMPLOYEE_NAME, TITLE, SUPERIOR_NO)
VALUES
	('A0100','00001', N'王總明', N'總經理', ''),
	('A0110','01001', N'陳啟明', N'經理', '00001'),
	('A0110','01010', N'蘇志成', N'組長', '01001'),
	('A0110','05101', N'詹姆士', N'專員', '01010'),
	('A0110','05102', N'林約翰', N'專員', '01010'),
	('A0120','02001', N'王麗莎', N'經理', '00001'),
	('A0120','02010', N'吳明識', N'組長', '02001'),
	('A0120','05201', N'簡東昕', N'專員', '02010'),
	('A0120','05202', N'鄭經任', N'專員', '02010');
--查詢
SELECT * FROM EMPLOYEE;

--遞迴SQL語法
;WITH EMPLOYEE_CTE AS (
	--找出最高層級
	SELECT EMPLOYEE_NO, EMPLOYEE_NAME, TITLE, SUPERIOR_NO, 1 AS EMPLOYEE_LEVEL
	FROM EMPLOYEE WHERE SUPERIOR_NO = ''
	UNION ALL  --一定要union all
	--跟 CTE 自身做遞迴 JOIN 
	SELECT A.EMPLOYEE_NO, A.EMPLOYEE_NAME, A.TITLE, A.SUPERIOR_NO
		, (B.EMPLOYEE_LEVEL + 1) AS EMPLOYEE_LEVEL --職位層級+1
	FROM EMPLOYEE A
	INNER JOIN EMPLOYEE_CTE B ON A.SUPERIOR_NO = B.EMPLOYEE_NO
)
SELECT * FROM EMPLOYEE_CTE ORDER BY EMPLOYEE_LEVEL

result:
00001	王總明	總經理		  1
01001	陳啟明	經理	00001	2
02001	王麗莎	經理	00001	2
02010	吳明識	組長	02001	3
01010	蘇志成	組長	01001	3
05101	詹姆士	專員	01010	4
05102	林約翰	專員	01010	4
05201	簡東昕	專員	02010	4
05202	鄭經任	專員	02010	4